#
#   Copyright 2019  SenX S.A.S.
#
#   Licensed under the Apache License, Version 2.0 (the "License");
#   you may not use this file except in compliance with the License.
#   You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
#   Unless required by applicable law or agreed to in writing, software
#   distributed under the License is distributed on an "AS IS" BASIS,
#   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#   See the License for the specific language governing permissions and
#   limitations under the License.
#

{
    'desc' 'Convert macro that takes as argument a tick and a (sub-)GTS into a mapper.'
    'sig' [ [ [ 'macro:MACRO' ]  [ 'mapper:MAPPER' ] ] ]
    'name' 'senx/gts/GTSMAPPER'
}
'.info' STORE

<%
    !$.info INFO

    '...macro' STORE

    <%
        SAVE 'context' STORE

        LIST-> DROP [ 'cur' 'c' 'l' 't' 'lat' 'lon' 'elev' 'v' ] STORE
        $t $lat $lon $elev $v MAKEGTS
        $c 0 GET RENAME
        $l 0 GET RELABEL

        $cur SWAP $...macro EVAL

        $context RESTORE
    %>
    MACROMAPPER
%>

//
// Unit tests
//

SAVE '.context' STORE
'GTSMAPPER' STORE

[
    [ 1 100 <% %> FOR ] [] [] [] [ 1 100 <% %> FOR ] MAKEGTS
    <% true MUSIGMA * SWAP NaN NaN NaN 5 ROLL %> @GTSMAPPER
    5 0 0
] MAP DROP

//
// Push macro
//

$GTSMAPPER
$.context RESTORE